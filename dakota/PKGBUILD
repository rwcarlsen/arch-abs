# Maintainer: Robert Carlsen <rwcarlsen@gmail.com>
pkgname=dakota
pkgver=6.2.0
pkgrel=1
pkgdesc="large-scale engineering optimization and uncertainty analysis package"
arch=('any')
url="http://dakota.sandia.gov"
license=('LGPL')
groups=('Sandia National Laboratories')
source=(https://dakota.sandia.gov/sites/default/files/distributions/public/dakota-6.2-public.src.tar.gz)
md5sums=('bc22ec578ae0a33f81d2922f4a3edfd8')
makedepends=('cmake>=2.8' 'gcc-fortran')
optdepends=('openmotif' 'texlive-core' 'doxygen')
depends=('boost' 'blas' 'lapack' 'python2')

build() {
  cd $srcdir
  rm -Rf $pkgname-$pkgver
  mv dakota-${pkgver}.src "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  rm -Rf build
  mkdir build; cd build;

  # dakota assumes python == python2 and this causes problems so disable it
  sed -i 's/include(FindPythonInterp)//' $srcdir/$pkgname-$pkgver/CMakeLists.txt

  # newer boost serialization versions don't work with dakota's surfpack dependency
  sed -i 's/\(option(HAVE_BOOST_SERIALIZATION "Use Boost serialization library" \)ON/\1OFF/' $srcdir/$pkgname-$pkgver/packages/surfpack/CMakeLists.txt

  cmake $srcdir/$pkgname-$pkgver -DBOOST_SIGNALS_NO_DEPRECATION_WARNING=true -DCMAKE_INSTALL_PREFIX=/usr
  make -j 4 dakota
}

check() {
  echo ""
  # cd "$srcdir/$pkgname-$pkgver/build/test"
  # make dakota-diffs-clean
  # ctest -j 2 -R dakota_
  # make dakota-diffs
}

package() {
  cd "$srcdir/$pkgname-$pkgver/build"
  make DESTDIR="$pkgdir/" install

  # dakota puts all sorts of files in the wrong places - clean them up:
  cd $pkgdir/usr
  mkdir -p share/dakota
  mv lib/cmake share/dakota/cmake
  mv bin/dakota.input.* share/dakota/
  mv COPYRIGHT INSTALL LICENSE README share/dakota/
  mv examples share/dakota/examples
  mv test share/dakota/test
  mkdir tmp
  mv include/* tmp/
  mkdir -p include/dakota
  mv tmp/* include/dakota/
  rm -Rf tmp
}

