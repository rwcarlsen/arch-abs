# Maintainer: Daniel Ehlers <danielehlers@mindeye.net>
pkgname=coin-osi
pkgver=0.106.0
pkgrel=2
pkgdesc="Osi (Open Solver Interface) provides an abstract base class to a generic linear programming (LP) solver, along with derived classes for specific solvers."
arch=('i686' 'x86_64')
url="https://projects.coin-or.org/Osi"
license=('EPL')
groups=('coin-or')
depends=('coin-utils>=2.7.1')
makedepends=('coin-utils>=2.7.1')
source=(http://www.coin-or.org/download/source/Osi/Osi-$pkgver.tgz)
md5sums=('e41d8d1c249565d3e58df55b35b4e789')

build() {
  cd "$srcdir/Osi-$pkgver"
  COIN_SKIP_PROJECTS="Sample" \
  ./configure --prefix=/usr \
              --with-coinutils-lib="`pkg-config --libs coinutils`" \
              --with-coinutils-incdir="/usr/include/coin/" -C
  make
}

check() {
  cd "$srcdir/Osi-$pkgver"
  make test
}

package() {
  cd "$srcdir/Osi-$pkgver"
  PKG_CONFIG_LIBDIR="${pkgdir}/usr/lib/pkgconfig/" \
  make DESTDIR="$pkgdir/" install
}
