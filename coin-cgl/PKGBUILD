# Maintainer: Daniel Ehlers <danielehlers@mindeye.net>
pkgname=coin-cgl
pkgver=0.58.0
pkgrel=2
pkgdesc="The COIN-OR Cut Generation Library (Cgl) is a collection of cut
generators"
arch=('i686' 'x86_64')
url="https://projects.coin-or.org/Cgl"
license=('EPL')
groups=('coin-or')
depends=('coin-utils>=2.9.0' 'coin-osi>=0.106.0' 'coin-clp>=1.15.0' )
makedepends=('coin-utils>=2.9.0' 'coin-osi>=0.106.0' 'coin-clp>=1.15.0' )
source=(http://www.coin-or.org/download/source/Cgl/Cgl-$pkgver.tgz)
md5sums=('e666374b09137a0db92c0db1709b9a26')

build() {
  cd "$srcdir/Cgl-$pkgver"
  COIN_SKIP_PROJECTS="Sample" \
  ./configure --prefix=/usr \
              --with-osi-lib="`pkg-config --libs osi`" \
              --with-osi-incdir="/usr/include/coin/" \
              --with-clp-lib="`pkg-config --libs clp`" \
              --with-clp-incdir="/usr/include/coin/" \
              --with-coinutils-lib="`pkg-config --libs coinutils`" \
              --with-coinutils-incdir="/usr/include/coin/" -C
          
  make
}

check() {
  cd "$srcdir/Cgl-$pkgver"
  make test
}

package() {
  cd "$srcdir/Cgl-$pkgver"
  PKG_CONFIG_LIBDIR="${pkgdir}/usr/lib/pkgconfig/" \
  make DESTDIR="$pkgdir/" install
}
