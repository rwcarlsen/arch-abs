# Maintainer: Robert Carlsen <rwcarlsen@gmail.com>
pkgname=coin-cbc
pkgver=2.8.0
pkgrel=3
pkgdesc="Cbc (Coin-or branch and cut) is an open-source mixed integer programming solver written in C++."
arch=('i686' 'x86_64')
url="https://projects.coin-or.org/Cbc"
license=('EPL')
groups=('coin-or')
source=(http://www.coin-or.org/download/source/Cbc/Cbc-$pkgver.tgz)
md5sums=('17118a8962e31476f0a2f6005a158697')
depends=('lapack' 'blas')
provides=('coin-clp' 'coin-cgl' 'coin-utils')
conflicts=('coin-clp' 'coin-cgl' 'coin-utils')

build() {
  cd "$srcdir/Cbc-$pkgver"
  ./configure --prefix=/usr -C
  make
}

check() {
  cd "$srcdir/Cbc-$pkgver"
  make test
}

package() {
  cd "$srcdir/Cbc-$pkgver"
  PKG_CONFIG_LIBDIR="${pkgdir}/usr/lib/pkgconfig/" \
  make DESTDIR="$pkgdir/" install
}
